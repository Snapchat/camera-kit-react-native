buildscript {
    // Buildscript is evaluated before everything else so we can't use getExtOrDefault
    def kotlin_version = rootProject.ext.has("kotlinVersion") ? rootProject.ext.get("kotlinVersion") : project.properties["CameraKitReactNative_kotlinVersion"]

    ext {
        cameraKitVersion = "1.28.0+d42a787e.1204"
        exoPlayerVersion = "2.16.1"
    }

    repositories {
        maven {
            url "gcs://snapengine-maven-publish/releases"
        }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:8.0.1"
        // noinspection DifferentKotlinGradleVersion
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

def isNewArchitectureEnabled() {
    return rootProject.hasProperty("newArchEnabled") && rootProject.getProperty("newArchEnabled") == "true"
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"

if (isNewArchitectureEnabled()) {
    apply plugin: "com.facebook.react"
}

def getExtOrDefault(name) {
    return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties["CameraKitReactNative_" + name]
}

def getExtOrIntegerDefault(name) {
    return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties["CameraKitReactNative_" + name]).toInteger()
}

android {

    namespace "com.snap.camerakit.reactnative"

    compileSdkVersion getExtOrIntegerDefault("compileSdkVersion")

    defaultConfig {
        minSdkVersion getExtOrIntegerDefault("minSdkVersion")
        targetSdkVersion getExtOrIntegerDefault("targetSdkVersion")

    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    lintOptions {
        disable "GradleCompatible"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    maven {
        url "gcs://snapengine-maven-publish/releases"
    }
}

def kotlin_version = getExtOrDefault("kotlinVersion")

dependencies {
    // For < 0.71, this will be from the local maven repo
    // For > 0.71, this will be replaced by `com.facebook.react:react-android:$version` by react gradle plugin
    //noinspection GradleDynamicVersion
    implementation "com.facebook.react:react-native:+"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation "com.google.android.exoplayer:exoplayer-core:$exoPlayerVersion"

    // Provides the core implementation of Camera Kit
    implementation "com.snap.camerakit:camerakit:$cameraKitVersion"
    // Provides an easy way to handle permission prompts
    implementation "com.snap.camerakit:support-permissions:$cameraKitVersion"
    // Implementation of Camera pipeline for Camera Kit using CameraX library
    implementation "com.snap.camerakit:support-camerax:$cameraKitVersion"
    // Provides an easy way to satisfy Snap's attribution requirement for Camera Kit applications
    implementation "com.snap.camerakit:support-snap-attribution:$cameraKitVersion"
}

